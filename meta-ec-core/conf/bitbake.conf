################################################################################
### Package default variables.
################################################################################

THISDIR = "${@os.path.dirname(d.getVar('FILE'))}"

PN = "${@bb.parse.vars_from_file(d.getVar('FILE', False),d)[0] or 'defaultpkgname'}"
PV = "${@bb.parse.vars_from_file(d.getVar('FILE', False),d)[1] or '1.0'}"
PR = "${@bb.parse.vars_from_file(d.getVar('FILE', False),d)[2] or 'r0'}"
PE = ""
PF = "${PN}-${PV}-${PR}"
P = "${PN}-${PV}"

BB_DEFAULT_TASK ?= "complete"

DISABLE_PKG_PROVIDERS_UPDATE ?= "0"
DISABLE_SUDO ?= "0"

################################################################################
### Package dependencies.
################################################################################

SRC_URI = ""
DEPENDS = ""
RDEPENDS = ""
PROVIDES = ""
PROVIDES:prepend = "${PN} "

################################################################################
### Default work and output directories for the build system.
################################################################################

FILESPATH = "${THISDIR}/${PF}:${THISDIR}/${P}:${THISDIR}/${PN}:${THISDIR}/files:${THISDIR}"

TMPDIR = "${TOPDIR}/tmp"
WORKDIR = "${TMPDIR}/work/${PN}/${PV}-${PR}"

T = "${WORKDIR}/temp"
S = "${WORKDIR}/${P}"
B = "${S}"

DEPLOY_DIR = "${TMPDIR}/deploy"
DL_DIR = "${TMPDIR}/downloads"
PERSISTENT_DIR = "${TMPDIR}/cache"
STAMP_DIR = "${TMPDIR}/stamps"
STAMP = "${STAMP_DIR}/${PN}/${PV}-${PR}/stamp"
STAMPCLEAN = "${STAMP_DIR}/${PN}/${PV}-${PR}/stamp*"

EC_TARGET_INSTALL_DIR = "${HOME}/.env-config"

################################################################################
### General helper variables.
################################################################################

ENVCONFIG_GENERATED_HEADER = " generated by env-config - do not edit"

################################################################################
### Export proxy-related variables for all the recipes
################################################################################

export http_proxy
export https_proxy
export no_proxy
export EC_PROXY_SERVER
export EC_PROXY_PORT
export EC_PROXY_USER
export EC_PROXY_PASSWORD

################################################################################
### Determine current distro and platform.
################################################################################

require conf/function/include/run_shell.inc

PLATFORM ?= "${@run_shell_script(d, 'which-platform').stdout.strip()}"
DISTRO ?= "${@run_shell_script(d, 'which-distro').stdout.strip()}"

################################################################################
### Include the rest of the config files.
################################################################################

include conf/local.conf
require conf/platform/${PLATFORM}.conf
require conf/distro/include/defaultsetup.inc
require conf/distro/${DISTRO}.conf

################################################################################
### Sudo availability check.
################################################################################

require conf/sudo.inc
DISABLE_SUDO_FORCED_REASON = "${@force_disable_sudo_if_required(d)}"
DISABLE_SUDO_FORCED = "${@'1' if d.getVar('DISABLE_SUDO_FORCED_REASON') else '0'}"

################################################################################
### Configure signature handling for stamps.
################################################################################

BB_SIGNATURE_HANDLER = "basichash"
